# Maintainer: WithTheBraid <info@braid.business>
# Co-Maintainer: Polarian <polarian@polarian.dev>, Fredy Garc√≠a <frealgagu at gmail dot com>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgbase=flutter
pkgver=3.19.4
_dartver=3.3.2
_enginever=a5c24f538d05aaf66f7972fb23959d8cafb9f95a
_materialfontsver=3012db47f3130e62f7cc0beabff968a33cbec8d8
_gradlewver=fd5c1f2c013565a3bea56ada6df9d2b8e96d56aa
_flutterarch=$(uname -m | sed s/aarch64/arm64/ | sed s/x86_64/x64/)
# this host is blocked in China, according to Flutter docs, the FLUTTER_STORAGE_BASE_URL environment variable
# should be used to provide an alternative mirror
_storagebase="${FLUTTER_STORAGE_BASE_URL:-"https://storage.googleapis.com"}"
pkgrel=10
pkgdesc="A new mobile app SDK to help developers and designers build modern mobile apps for iOS and Android."
_pkgdesc="Flutter SDK component"
arch=("x86_64" "aarch64")
url="https://${pkgbase}.dev"
license=("custom" "BSD" "CCPL")
makedepends=(
	"dart>=${_dartver}"
	"jq"
	"gradle"
	"unzip"
)
options=("!emptydirs")
install="${pkgbase}.install"
source=(
  "${pkgbase}-${pkgver}.tar.xz::https://github.com/${pkgbase}/${pkgbase}/archive/refs/tags/${pkgver/.hotfix/+hotfix}.tar.gz"
  # material_fonts
  "material_fonts.zip::${_storagebase}/flutter_infra_release/flutter/fonts/${_materialfontsver}/fonts.zip"
  # gradle_wrapper
  # we use the arch system gradle to create that one
  # "gradle_wrapper.tar.gz::${_storagebase}/flutter_infra_release/gradle-wrapper/${_gradlewver}/gradle-wrapper.tgz"

  # engine/android-x86
  "android-x86.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x86/artifacts.zip"
  # engine/android-x64
  "android-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x64/artifacts.zip"
  # engine/android-arm
  "android-arm.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm/artifacts.zip"
  # engine/android-arm-profile
  "android-arm-profile.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm-profile/artifacts.zip"
  # engine/android-arm-release
  "android-arm-release.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm-release/artifacts.zip"
  # engine/android-arm64
  "android-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm64/artifacts.zip"
  # engine/android-arm64-profile
  "android-arm64-profile.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm64-profile/artifacts.zip"
  # engine/android-arm64-release
  "android-arm64-release.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm64-release/artifacts.zip"

  # engine/android-x64-profile
  "android-x64-profile.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x64-profile/artifacts.zip"
  # engine/android-x64-release
  "android-x64-release.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x64-release/artifacts.zip"
  # engine/android-x86-jit-release
  "android-x64-jit-release.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x86-jit-release/artifacts.zip"

  # flutter_web_sdk
  "flutter_web_sdk.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/flutter-web-sdk.zip"
  # pkg
  "sky_engine.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/sky_engine.zip"

  # engine/common
  "flutter_patched_sdk.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/flutter_patched_sdk.zip"
  # engine/common
  "flutter_patched_sdk_product.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/flutter_patched_sdk_product.zip"

  "system-dart.patch"
  "gradle-user-home.patch"

  # thanks to lauren n. liberda from Alpine for the awesome patchset used here !
  "${pkgbase}.sh"
  "version.patch"
  "no-lock.patch"
  "no-runtime-download.patch"
  "doctor.patch"
  "opt-in-analytics.patch"
)
source_x86_64=(
  # engine/android-arm-profile/linux-x64
  "android-arm-profile-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm-profile/linux-x64.zip"
  # engine/android-arm-release/linux-x64
  "android-arm-release-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm-release/linux-x64.zip"
  # engine/android-arm64-profile/linux-x64
  "android-arm64-profile-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm64-profile/linux-x64.zip"
  # engine/android-arm64-release/linux-x64
  "android-arm64-release-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-arm64-release/linux-x64.zip"
  # engine/android-x64-profile/linux-x64
  "android-x64-profile-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x64-profile/linux-x64.zip"
  # engine/android-x64-release/linux-x64
  "android-x64-release-linux-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/android-x64-release/linux-x64.zip"

  # engine/linux-$ARCH
  "engine-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-x64/artifacts.zip"
  # engine/linux-$ARCH
  "gtk-debug-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-x64-debug/linux-x64-flutter-gtk.zip"
  # engine/linux-$ARCH-profile
  "gtk-profile-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-x64-profile/linux-x64-flutter-gtk.zip"
  # engine/linux-$ARCH-release
  "gtk-release-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-x64-release/linux-x64-flutter-gtk.zip"
  # engine/linux-$ARCH
  "font-subset-x64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-x64/font-subset.zip"
)
source_aarch64=(
  # engine/linux-$ARCH
  "engine-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-arm64/artifacts.zip"
  # engine/linux-$ARCH
  "gtk-debug-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-arm64-debug/linux-arm64-flutter-gtk.zip"
  # engine/linux-$ARCH-profile
  "gtk-profile-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-arm64-profile/linux-arm64-flutter-gtk.zip"
  # engine/linux-$ARCH-release
  "gtk-release-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-arm64-release/linux-arm64-flutter-gtk.zip"
  # engine/linux-$ARCH
  "font-subset-arm64.zip::${_storagebase}/flutter_infra_release/flutter/${_enginever}/linux-arm64/font-subset.zip"
)
noextract=(
  "material_fonts.zip"
  # "gradle_wrapper.tar.gz"

  "android-x86.zip"
  "android-x64.zip"
  "android-arm.zip"
  "android-arm-profile.zip"
  "android-arm-release.zip"
  "android-arm64.zip"
  "android-arm64-profile.zip"
  "android-arm64-release.zip"

  "android-x64-profile.zip"
  "android-x64-release.zip"
  "android-x64-jit-release.zip"

  "flutter_web_sdk.zip"
  "sky_engine.zip"
  "flutter_patched_sdk.zip"
  "flutter_patched_sdk_product.zip"

  # x64
  "android-arm-profile-linux-x64.zip"
  "android-arm-release-linux-x64.zip"
  "android-arm64-profile-linux-x64.zip"
  "android-arm64-release-linux-x64.zip"
  "android-x64-profile-linux-x64.zip"
  "android-x64-release-linux-x64.zip"

  "engine-x64.zip"
  "gtk-debug-x64.zip"
  "gtk-profile-x64.zip"
  "gtk-release-x64.zip"
  "font-subset-x64.zip"

  # arm64
  "engine-arm64.zip"
  "gtk-debug-arm64.zip"
  "gtk-profile-arm64.zip"
  "gtk-release-arm64.zip"
  "font-subset-arm64.zip"
)



sha256sums=('089f924c72f28d25851382d70db83df83c64746713f6a8ca08879a1530adb8ca'
            'e56fa8e9bb4589fde964be3de451f3e5b251e4a1eafb1dc98d94add034dd5a86'
            'd5da421c4849c169766be16f51e2634e02f1536416befb61f5f6551fb60c32d9'
            'ec7e0d94b5fa32efe772e246893e9cc832e1f133e737f498fb7f7da5b84192c2'
            'e5be895a4e8d2bf7d553839eeb097aff109fcc2efc45d76d7feebb18855831c8'
            'e56fa8dfcaa3830eaa616c66faae0910a32df772d01a2d68d3fa2c895e6d65ff'
            '3921c7e7f95e6d95731fbc673da5e692da006738f3667a7db0295cb5ee7c1632'
            '3e7f49b80b31db50b6b74fc91a4d9a69407ebb1b1a33a6525e3e6dad0a397d99'
            'e82408b2c0d7fa57f490a03492bfd9110a8ef923d4f22417fa99fd8854022dc8'
            'c00b0d2b848dbbb184d263e7be87225219cf6d13fc2193d986c8684630b18fac'
            'b50fed99cd5f6ae41282beab6c97380832aa0a1b81c261fe855bbff7ca95e9d3'
            '69153dd195a753bafd0f06ac0991cc92d408590e7b1dc398370dd4aa0255f614'
            '5a38c17cfc0e9aef522d20b6b889462b340cc0430ece8d9a456b947d0cb731e4'
            'efa93f450398060b49a89b04caeac7d4a8bf2c3870170e6b619acaf0ffb0337b'
            'b1f8faafcc0f3492aa1d0dab511e9a026de19356e35c55a237b8cfa3c0b1bcf1'
            '0a52880f8f3aff667db1a29ec2e68567689b378e773345427f9e5cf6cb80dea3'
            '2e848e6ee2dd591c0e558ee37d80391e4acb5bf8672cbe3e02af9a52d39e25b8'
            'd721fc48f534af8f804bb4a9f2cb1d304627a9f73881b3f61d829a9f1e33164f'
            'de0d3567d83bd756841b19ccf879efc02749d8a45cf18d94cd71ec1d366c9024'
            '54db9347ac6467b806fff70f62b2709276a0ca4d82468ae8357d5520db0ad04a'
            '688a7d6a3c220cf09f7e48af46f1ef1b01d251679962c825eded0b3fa4fc2ab1'
            '544d08716332a9f9358b21010d468b84a9edff0da7bbb1baf0cf4d6322821ea5'
            'a5f19e68e9e4790d017dc4988e715f51c44548df5615aae6106d1a0c84fe49f1'
            '04531ee1732c18c933b5b28f5da88ed183d5aa3698b1d1e912c000928b93ec91'
            '1578e819b6ee479b6db7a095bcfa74372d3ff555642c6d6ea7112e97bb6f2027')
sha256sums_x86_64=('f41f8cf16ed627f94c9ad2c60602fdedb585ff1f8d50f4c44927da706dbe1808'
                   '9032d360fb258fba392fd00a150881674bfa88598c6fad2c73f552a03501f1ce'
                   '33517b19738cf68a4c1de7566d3602a09b3be037b9f4aca17f10a364b476cb9e'
                   '29780ca6efbcdabe6988cd7eae6dbf995c5839d3f4c0b1f3ee6de2f9bfdc6c5c'
                   '6a35206886a116ba4ae83432035b877818b883033a70697a8330c473f9374b8e'
                   '1778a36d34657f853b1c6cb848c5dae6a1480babd357131ab723d8b41109ffd3'
                   'c72981b583838b6016f3ab5bf5240f041acefc82c3f67983e447373b520e54e5'
                   '214302538b9c983bc075cb59770329f4f751f2a0f73c390566f68d670cdcdb6f'
                   '70edb9c63fe08d77cb2a9b59f9d4d1ec16e28a49737e8dd0cdbf5ccefcc562bb'
                   '6f5d86f207c78cb64744889c34ed3ac45e403ea035d512674e5b348cb0d6355a'
                   '05778e62c2da50d8280dc55589225871bf90bb2d543b48d2f1d6acd73460d573')
sha256sums_aarch64=('7f99e5020f79d3a9a369e579543db860afef6f2a41403992733a2b707b73e757'
                    '10ae30dac5b3ed1cc820a2d0ea9796ce2fa0a6e87813d212a15e9d595a1c3afc'
                    'ddc83cb35141192222529db34e57e26aef70b3960d4c688b73121a434d580232'
                    '65500e40641c62f470d36034e3579c87108964660788d67fc3d771be66b1a096'
                    '6e0044cf445b8650efe57daf9415adb99f063c7a529437f2dbe58c7774e57cab')

prepare() {
  mv "${srcdir}/${pkgbase}-${pkgver/.hotfix/+hotfix}" "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/system-dart.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/gradle-user-home.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/version.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/no-lock.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/no-runtime-download.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/doctor.patch" -d "${srcdir}/${pkgbase}"
  patch -p1 -i "${srcdir}/opt-in-analytics.patch" -d "${srcdir}/${pkgbase}"

  echo "${pkgver}" > "${srcdir}/${pkgbase}/version"
  mkdir -p "${srcdir}/${pkgbase}/bin/cache/artifacts"
  cat > "${srcdir}/${pkgbase}/bin/cache/flutter.version.json" <<EOF
{
	"frameworkVersion": "$pkgver",
	"channel": "$_channel",
	"repositoryUrl": "https://github.com/flutter/flutter.git",
	"frameworkRevision": "archlinuxaur0000000000000000000000000000",
	"frameworkCommitDate": "2038-01-19 03:14:08",
	"engineRevision": "$(cat "${srcdir}/${pkgbase}/bin/internal/engine.version")",
	"dartSdkVersion": "$_dartver",
	"devToolsVersion": $(jq '.version' < /opt/dart-sdk/bin/resources/devtools/version.json),
	"flutterVersion": "$pkgver"
}
EOF

  mkdir "${srcdir}/gradlew"
  pushd ${srcdir}/gradlew
  gradle init --type basic --project-name flutter --dsl kotlin --incubating
  gradle wrapper
  popd

  cd "${srcdir}/${pkgbase}/bin/cache"

  unzip -o -q "${srcdir}/flutter_web_sdk.zip" -d flutter_web_sdk
  unzip -o -q "${srcdir}/sky_engine.zip" -d pkg

  cd "${srcdir}/${pkgbase}/bin/cache/artifacts"

  # why should we use a pre-build gradle wrapper if we have it in the arch repos ?
  mkdir -p gradle_wrapper/gradle
  cp -pr "${srcdir}/gradlew/gradle/wrapper" gradle_wrapper/gradle
  cp -pr "${srcdir}/gradlew/gradlew" gradle_wrapper

  # mkdir "gradle_wrapper"
  # tar -xzf "${srcdir}/gradle_wrapper.tar.gz" -C "gradle_wrapper"
  unzip -o -q "${srcdir}/material_fonts.zip" -d "material_fonts"

  mkdir -p engine/android-arm-profile
  mkdir -p engine/android-arm64-profile
  mkdir -p engine/android-x64-profile
  mkdir -p engine/android-arm-release
  mkdir -p engine/android-arm64-release
  mkdir -p engine/android-x64-release

  if [ "$(uname -m)" == "x86_64" ]; then

  unzip -o -q "${srcdir}/android-arm-profile-linux-x64.zip" -d engine/android-arm-profile/linux-x64
  unzip -o -q "${srcdir}/android-arm-release-linux-x64.zip" -d engine/android-arm-release/linux-x64
  unzip -o -q "${srcdir}/android-arm64-profile-linux-x64.zip" -d engine/android-arm64-profile/linux-x64
  unzip -o -q "${srcdir}/android-arm64-release-linux-x64.zip" -d engine/android-arm64-release/linux-x64
  unzip -o -q "${srcdir}/android-x64-profile-linux-x64.zip" -d engine/android-x64-profile/linux-x64
  unzip -o -q "${srcdir}/android-x64-release-linux-x64.zip" -d engine/android-x64-release/linux-x64

  fi

  unzip -o -q "${srcdir}/android-x86.zip" -d engine/android-x86
  unzip -o -q "${srcdir}/android-x64.zip" -d engine/android-x64
  unzip -o -q "${srcdir}/android-arm.zip" -d engine/android-arm
  unzip -o -q "${srcdir}/android-arm-profile.zip" -d engine/android-arm-profile
  unzip -o -q "${srcdir}/android-arm-release.zip" -d engine/android-arm-release
  unzip -o -q "${srcdir}/android-arm64.zip" -d engine/android-arm64
  unzip -o -q "${srcdir}/android-arm64-profile.zip" -d engine/android-arm64-profile
  unzip -o -q "${srcdir}/android-arm64-release.zip" -d engine/android-arm64-release

  unzip -o -q "${srcdir}/android-x64-profile.zip" -d engine/android-x64-profile
  unzip -o -q "${srcdir}/android-x64-release.zip" -d engine/android-x64-release
  unzip -o -q "${srcdir}/android-x64-jit-release.zip" -d engine/android-x86-jit-release

  unzip -o -q "${srcdir}/flutter_patched_sdk.zip" -d engine/common
  unzip -o -q "${srcdir}/flutter_patched_sdk_product.zip" -d engine/common

  unzip -o -q "${srcdir}/engine-${_flutterarch}.zip" -d engine/linux-${_flutterarch}
  unzip -o -q "${srcdir}/gtk-debug-${_flutterarch}.zip" -d engine/linux-${_flutterarch}
  unzip -o -q "${srcdir}/gtk-profile-${_flutterarch}.zip" -d engine/linux-${_flutterarch}-profile
  unzip -o -q "${srcdir}/gtk-release-${_flutterarch}.zip" -d engine/linux-${_flutterarch}-release
  unzip -o -q "${srcdir}/font-subset-${_flutterarch}.zip" -d engine/linux-${_flutterarch}
}

build() {
  export PUB_CACHE="${srcdir}/${pkgbase}/pub-cache"
  cd "${srcdir}/${pkgbase}"
  dart pub get -C "packages/flutter_tools" --no-offline --no-precompile
  dart --verbosity=error --disable-dart-dev \
		--snapshot="bin/cache/flutter_tools.snapshot" --snapshot-kind="app-jit" \
		--packages="packages/flutter_tools/.dart_tool/package_config.json" \
		--no-enable-mirrors "packages/flutter_tools/bin/flutter_tools.dart" --version
 cd ../..

  sed -Ei 's|'"$PUB_CACHE"'|/usr/lib/flutter/pub-cache|g' "${srcdir}/${pkgbase}/packages/flutter_tools/.dart_tool/package_config.json"
  find "$PUB_CACHE" -name '*.aot' -delete
}

_package() {
  pkgdesc="${_pkgdesc} - full installation of development tool and runtime"
  depends=("${pkgbase}-devel" "${pkgbase}-target-linux" "${pkgbase}-target-android" "${pkgbase}-target-web" "${pkgbase}-intellij-patch")
}

_package-common() {
  pkgdesc="${_pkgdesc} - common SDK files and pub cache"

  install -Dm644 "${srcdir}/${pkgbase}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_driver"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_goldens"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_localizations"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_test"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_web_plugins"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/fuchsia_remote_debug_protocol"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/integration_test"

  cp -ra "${srcdir}/${pkgbase}/packages/flutter/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_driver/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_driver"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_goldens/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_goldens"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_localizations/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_localizations"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_test/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_test"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_web_plugins/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_web_plugins"
  cp -ra "${srcdir}/${pkgbase}/packages/fuchsia_remote_debug_protocol/"{pubspec.yaml,lib} "${pkgdir}/usr/lib/${pkgbase}/packages/fuchsia_remote_debug_protocol"
  cp -ra "${srcdir}/${pkgbase}/packages/integration_test/"{pubspec.yaml,lib,android} "${pkgdir}/usr/lib/${pkgbase}/packages/integration_test"
}

_package-engine-common-google-bin() {
  pkgdesc="${_pkgdesc} - common engine files"
  depends=(
	"${pkgbase}-common"
	"${pkgbase}-sky-engine"
	"${pkgbase}-material-fonts"
  )
  provides=(
	"${pkgbase}-engine-common"
  )
  conflicts=(
	"${pkgbase}-engine-common"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/internal"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/common" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine/common"

  install -Dm644 "${srcdir}/${pkgbase}/bin/internal/engine.version" "${pkgdir}/usr/lib/${pkgbase}/bin/internal/engine.version"
  install -Dm644 "${srcdir}/${pkgbase}/bin/internal/engine.realm" "${pkgdir}/usr/lib/${pkgbase}/bin/internal/engine.realm"

}

_package-sky-engine-google-bin() {
  pkgdesc="${_pkgdesc} - sky-engine"
  depends=(
	"${pkgbase}-common"
  )
  provides=(
	"${pkgbase}-sky-engine"
  )
  conflicts=(
	"${pkgbase}-sky-engine"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/pkg" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/pkg"
}

_package-material-fonts() {
  pkgdesc="${_pkgdesc} - material fonts"
  depends=(
	"${pkgbase}-common"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/material_fonts" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/material_fonts"
}

_package-target-linux() {
  pkgdesc="${_pkgdesc} - linux target files"
  depends=(
	"${pkgbase}-tool"
	"${pkgbase}-engine-linux"
	"dart>=${_dartver}"
	"clang"
	"cmake"
        "ninja"
	"pkgconf" # base-devel, but runtime dependency
	# runtime shared libraries
	"gtk3"
	"libglvnd" # https://github.com/flutter/engine/pull/16924
  )


  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/bin"

  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/bin/tool_backend.sh" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/bin"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/bin/tool_backend.dart" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/bin"
}

_package-engine-linux-google-bin() {
  pkgdesc="${_pkgdesc} - linux engine"
  depends=(
	"${pkgbase}-engine-common"
  )
  provides=(
	"${pkgbase}-engine-linux"
  )
  conflicts=(
	"${pkgbase}-engine-linux"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/linux-${_flutterarch}" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/linux-${_flutterarch}-profile" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/linux-${_flutterarch}-release" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
}

_package-target-web() {
  pkgdesc="${_pkgdesc} - web target files"
  depends=(
	"${pkgbase}-tool"
	"${pkgbase}-engine-web"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/lib/src/web"

  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/lib/src/web/file_generators" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/lib/src/web"
}

_package-engine-web-google-bin() {
  pkgdesc="${_pkgdesc} - web engine"
  depends=(
	"${pkgbase}-engine-common"
  )
  provides=(
	"${pkgbase}-engine-web"
  )
  conflicts=(
	"${pkgbase}-engine-web"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/flutter_web_sdk" "${pkgdir}/usr/lib/${pkgbase}/bin/cache"
}

_package-target-android() {
  pkgdesc="${_pkgdesc} - android target files"
  depends=(
	"${pkgbase}-tool"
	"${pkgbase}-engine-android"
  )
  optdepends=("android-sdk: develop for Android devices"
	    "java-environment: develop for Android devices"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/gradle_wrapper" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/gradle" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools"
}


_package-engine-android-google-bin() {
  pkgdesc="${_pkgdesc} - android engine"
  depends=(
	"${pkgbase}-engine-common"
  )
  provides=(
	"${pkgbase}-engine-android"
  )
  conflicts=(
	"${pkgbase}-engine-android"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm-release" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm-profile" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm64" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm64-release" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-arm64-profile" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-x64" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-x64-release" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-x64-profile" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-x86" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/artifacts/engine/android-x86-jit-release" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/artifacts/engine"
}

_package-tool() {
  pkgdesc="${_pkgdesc} - CLI tool (for packaging only)"
  depends=(
	"${pkgbase}-common"
	# TODO: completely compile Flutter tool standalone and drop dependency
	"dart>=${_dartver}"
	# commands first
	"bash"
	"curl"
	"file" # base-devel, but runtime dependency
	"git"
	"coreutils" # explicit dependency to mkdir, rm
	"unzip"
	"which" # base-devel, but runtime dependency
	"xz"
	"zip"
	"glu" # libGLU.so.1 required for flutter test
  )


  install -dm755 "${pkgdir}/usr/lib/${pkgbase}"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/.dart_tool"

  cp -ra "${srcdir}/${pkgbase}/bin/cache/flutter_tools.snapshot" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/flutter_tools.snapshot"
  cp -ra "${srcdir}/${pkgbase}/bin/cache/flutter.version.json" "${pkgdir}/usr/lib/${pkgbase}/bin/cache"
  cp -ra "${srcdir}/${pkgbase}/version" "${pkgdir}/usr/lib/${pkgbase}"

  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/.dart_tool/package_config.json" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools/.dart_tool"

  install -dm755 "${pkgdir}/usr/bin"
  install -Dm755 "${srcdir}/${pkgbase}.sh" "${pkgdir}/usr/lib/${pkgbase}/bin/flutter"
  ln -sf "/usr/lib/flutter/bin/flutter" "${pkgdir}/usr/bin/flutter"
}

_package-devel() {
  pkgdesc="${_pkgdesc} - CLI tool (for application development)"
  depends=(
	"${pkgbase}-tool"
	"dart>=${_dartver}"
  )
  replaces=("${pkgbase}-tool-developer")

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}"
  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools"

  cp -ra "${srcdir}/${pkgbase}/examples" "${pkgdir}/usr/lib/${pkgbase}"
  cp -ra "${srcdir}/${pkgbase}/packages/flutter_tools/templates" "${pkgdir}/usr/lib/${pkgbase}/packages/flutter_tools"
  
  # TODO: patch `flutter create` to run without pub cache
  cp -ra "${srcdir}/${pkgbase}/pub-cache" "${pkgdir}/usr/lib/${pkgbase}/pub-cache"
}

_package-intellij-patch() {
  pkgdesc="${_pkgdesc} - IntelliJ Flutter plugin hotfix"
  depends=("${pkgbase}-common")
  optdepends=(
	"android-studio"
        "intellij-idea-community-edition"
        "intellij-idea-ultimate-edition"
  )

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/bin/cache"
  
  ln -sf "${DART_ROOT:-"/opt/dart-sdk"}/bin/dart" "${pkgdir}/usr/lib/${pkgbase}/bin/dart"
  # * not my fault grumble * : The IntelliJ Flutter plugin enforces this relative Dart SDK
  ln -sf "${DART_ROOT:-"/opt/dart-sdk"}" "${pkgdir}/usr/lib/${pkgbase}/bin/cache/dart-sdk"
}

pkgname=("${pkgbase}" "${pkgbase}-common" "${pkgbase}-engine-common-google-bin" "${pkgbase}-engine-linux-google-bin" "${pkgbase}-engine-web-google-bin" "${pkgbase}-engine-android-google-bin" "${pkgbase}-sky-engine-google-bin" "${pkgbase}-material-fonts" "${pkgbase}-tool" "${pkgbase}-devel" "${pkgbase}-target-linux" "${pkgbase}-target-android" "${pkgbase}-target-web" "${pkgbase}-intellij-patch")

for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

